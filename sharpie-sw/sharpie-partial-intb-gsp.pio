.program sharpie_partial_intb_gsp
.pio_version 1
.side_set 2
; Sharpie partial update PIO: INTB+GSP controller
; (the start signals for one video frame)

; this is the master SM, so to speak. the CPU sets an IRQ to tell this
; one to start (INTB and GSP come before any other signals in one
; frame)

; side-set: 0b[GSP][INTB]
; clock: 1/32 of a full (not skipped) GCK h/l
; expects to be able to pull one 32-bit int, a big counter value
; so that INTB falls correctly

; initially we wanted to use 1/4 of a GCK h/l as the clock, but that
; makes it so you can't align INTB's fall correctly. shifting to 1/32
; means that we have to put it in a separate PIO, but that's fine
; because we were using two PIOs anyway.

.wrap_target
wait 1 irq 0     side 0b00     ; wait for CPU to tell us to start
out x, 32        side 0b01 [7] ; rise INTB, get value of main counter

nop              side 0b11 [7] ; rise GSP
out y, 32        side 0b11     ; wait more GSP, to meet thsGSP timing, and get loop counter
irq set 1        side 0b11     ; wait rest of GSP and tell GCK + GCK end to start

; it's easier and smaller to wait out all of GSP with a loop instead of with
; a bunch of instructions
gsploop:
jmp y--, gsploop side 0b11

loop:
jmp x--, loop    side 0b01     ; just hang out, man

; literally just wrap, it'll wait for irq 0
.wrap


% c-sdk {
#include "hardware/gpio.h"
static inline void sharpie_partial_intb_gsp_pio_init(PIO pio, uint sm, uint offset, uint intb_pin) {
  pio_gpio_init(pio, intb_pin);
  pio_gpio_init(pio, intb_pin + 1);
  
  pio_sm_set_consecutive_pindirs(pio, sm, intb_pin, 2, true); // set all pins output
  
  pio_sm_config c = sharpie_partial_intb_gsp_program_get_default_config(offset);

  // set side-set pins starting from intb_pin
  sm_config_set_sideset_pins(&c, intb_pin);
  sm_config_set_clkdiv(&c, 387.5); // we need the fast clock to get stuff aligned
  
  // shift right, autopull on, autopull threshold 32
  sm_config_set_out_shift(&c, true, true, 32);

  pio_sm_init(pio, sm, offset, &c);
  pio_sm_set_enabled(pio, sm, true);
}

%}